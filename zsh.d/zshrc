# ============================================================
#  基本設定 / General
# ============================================================
export LANG=ja_JP.UTF-8
export EDITOR="nvim"

# ============================================================
#  Zinit 本体（プラグインマネージャ）
#  ※ すべての zinit プラグインを使う前にロードする必要あり
# ============================================================
### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})…%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# --- Zinit annex（zinit の拡張モジュール）---
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust
### End of Zinit's installer chunk

# ============================================
# Zinit プラグイン読み込み（外部ファイル）
# ============================================
if [[ -f "$HOME/.zsh_plugins.zsh" ]]; then
  source "$HOME/.zsh_plugins.zsh"
fi


# ============================================================
#  エイリアス / Aliases
# ============================================================
if [ -f ~/.zsh_aliases ]; then
    source ~/.zsh_aliases
fi

# --- ローカル設定（マシンごとに変更したいもの）---
if [ -f ~/.zshrc_local ]; then
  source ~/.zshrc_local
fi


# ============================================================
#  シェル基本挙動 / Shell behavior
# ============================================================
setopt autocd  # ディレクトリ名だけで移動（cd 省略）


# ============================================================
#  履歴設定 / History
# ============================================================
HISTFILE=~/.zsh_history
HISTSIZE=50000
SAVEHIST=50000

# 履歴ノイズを減らす（不要コマンド）
HISTIGNORE='history:pwd:ls:ll:exit:dot:Tmp:vv:git:gg'

setopt INC_APPEND_HISTORY      # 実行直後に履歴追記
setopt SHARE_HISTORY           # シェル間で履歴共有
setopt HIST_IGNORE_DUPS        # 連続重複を1つに
setopt HIST_REDUCE_BLANKS      # 余計な空白削除


# ============================================================
#  補完設定 / Completion
# ============================================================
autoload -Uz compinit
compinit

zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'  # 大文字小文字無視
setopt COMPLETE_IN_WORD       # 単語途中でも補完
setopt COMPLETE_ALIASES       # alias でも補完を効かせる


# ============================================================
#  Starship プロンプト / Prompt
# ============================================================
export STARSHIP_CONFIG="$HOME/.config/starship.toml"

# --- vi モード（zle）---
bindkey -v
bindkey -M viins 'jj' vi-cmd-mode  # Insert で jj → Normal

# ============================================================
#  ZLE / Vim モード調整（履歴移動時に行頭へ）
# ============================================================
# Normal モードで `k` を押したとき:
#   1. 1つ上の履歴に移動（up-line-or-history）
#   2. カーソルを行頭へ移動（beginning-of-line）
function up_history_bol() {
  zle up-line-or-history
  zle beginning-of-line
}
zle -N up_history_bol
bindkey -M vicmd 'k' up_history_bol

# --- Starship 初期化（多重初期化防止）---
if [[ -z ${STARSHIP_INIT_DONE-} ]]; then
  eval "$(starship init zsh)"
  typeset -g STARSHIP_INIT_DONE=1
fi

# --- カーソル形状をモードで出し分け ---
autoload -Uz add-zle-hook-widget
function _cursor_by_keymap {
  case $KEYMAP in
    vicmd)      printf '\e[1 q' ;;  # Normal: ブロック
    viins|main) printf '\e[5 q' ;;  # Insert: ビーム
  esac
}
add-zle-hook-widget zle-keymap-select _cursor_by_keymap
add-zle-hook-widget zle-line-init _cursor_by_keymap
printf '\e[5 q'  # 起動直後（Insert）


# ============================================================
#  ツール連携 / Tools
# ============================================================

# zoxide（高速 cd）
eval "$(zoxide init zsh)"

# Homebrew PATH
export PATH="/opt/homebrew/bin:$PATH"
export PATH="/opt/homebrew/sbin:$PATH"

# pyenv 設定
export PATH="$HOME/.pyenv/bin:$PATH"
if command -v pyenv 1>/dev/null 2>&1; then
  eval "$(pyenv init --path)"
  eval "$(pyenv init -)"
fi


# ============================================================
#  ユーティリティ関数 / Utility Functions
# ============================================================

# --- fcp: ファイル内容を pbcopy ---
fcp() {
  if [[ -f "$1" ]]; then
    cat "$1" | pbcopy
    echo "Copied: $1"
  else
    echo "File not found: $1" >&2
  fi
}


# --- cdf: fzf でディレクトリ選択 → cd ---
cdf() {
  local dir

  # fd が入っていれば優先して使う（高速）
  if command -v fd >/dev/null 2>&1; then
    dir=$(fd -t d . 2>/dev/null | fzf --height 40% --reverse --ansi) || return
  else
    # fd がない場合は find で代用（深さはお好みで）
    dir=$(find . -type d -maxdepth 5 2>/dev/null | fzf --height 40% --reverse --ansi) || return
  fi

  cd "$dir" || return
  ls
}


# --- calc(): 隔離環境で IPython ---
calc() {
  local VENV="${CALC_VENV:-$HOME/.venvs/calc}"
  local -a IPY_OPTS=(--quick --no-banner --HistoryManager.enabled=False)

  if [[ ! -x "$VENV/bin/ipython" ]]; then
    echo "calc: $VENV/bin/ipython が見つかりません。まず install.sh を実行してください。" >&2
    return 1
  fi

  (
    source "$VENV/bin/activate"
    export VIRTUAL_ENV_DISABLE_PROMPT=1
    ipython "${IPY_OPTS[@]}"
    type deactivate >/dev/null 2>&1 && deactivate
  )
}


# ============================================================
#  fzf 連携 / fzf Integration
# ============================================================
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
